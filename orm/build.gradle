apply plugin: 'com.android.library'
apply plugin: 'signing'

apply plugin: 'maven-publish'
def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("local.properties")))
def getVersionName = { ->
    return "1.0.0" // Replace with version Name
}
def getArtificatId = { ->
    return "orm" // Replace with library name ID
}

publishing {
    publications {
        release(MavenPublication) {
            pom {
                name='Resqlity Android Library'
                description='Resqlity.Com Android Connector Library'
                url='https://resqlity.com'
                licenses {
                    license {
                        name = 'The MIT Licence'
                        url = 'https://github.com/Resqlity/Resqlity-Android/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'berkayalcin'
                        name = 'Berkay YALCIN'
                        email = 'yalcinberkaycom@gmail.com'
                    }
                }
            }
            groupId 'com.resqlity'
            artifactId getArtificatId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/${getArtificatId()}-release.aar")
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/resqlity/resqlity-android")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    useLibrary 'org.apache.http.legacy'

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    api "org.java-websocket:Java-WebSocket:1.5.1"


}